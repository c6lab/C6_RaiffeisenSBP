// (c) 2021 C6 Lab, Ramona Orlova, http://c6lab.org
// Все права сохранены.
////////////////////////////////////////////////////////////////////////////////

//
Процедура Команда_СБППолучитьQRКод(Форма, Источник) Экспорт
	Перем ЧекИнфо, СуммаОплаты;
	Если (НЕ ЗначениеЗаполнено(Источник.Ссылка)) Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения, "Документ не записан!", 6);
		Возврат;
	КонецЕсли;
	ЧекИнфо = ВернутьИнформациюПоЧекуККМ(Источник);
	C6_СБПВызовСервера.ВернутьИнформациюПоЧекуККМ(ЧекИнфо);
	Если (НЕ ЧекИнфо.ЕстьКодОплаты) Тогда
		СуммаОплаты = НайтиОплатуПоВидуВЧеке(Форма);
		Если (ЗначениеЗаполнено(СуммаОплаты)) Тогда
			ЧекИнфо.Вставить("НазначениеПлатежа", НазначениеПлатежаПоЧеку(ЧекИнфо));
			ЧекИнфо.ЕстьКодОплаты = Банк_РегистрацияQRКода(ЧекИнфо, СуммаОплаты);
		Иначе
			ДобавитьОплатуПоВидуВЧеке(Форма, ЧекИнфо.Сумма);
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(Новый ОписаниеОповещения, "QR-код уже сгенерирован, проверьте статус!", 6);
	КонецЕсли;
	Если (ЧекИнфо.ЕстьКодОплаты) Тогда
		C6_QRДисплей.ПоказатьКод(C6_СБПВызовСервера.ВернутьURLКодаОплаты(ЧекИнфо.КодОплаты));
	КонецЕсли;
КонецПроцедуры

//
Процедура Команда_СБПСтатусОплаты(Форма, Источник) Экспорт
	Перем ЧекИнфо;
	Перем СтатусКодаОплаты, СуммаПлатежа;
	Если (НЕ ЗначениеЗаполнено(Источник.Ссылка)) Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения, "Документ не записан!", 6);
		Возврат;
	КонецЕсли;
	ЧекИнфо = ВернутьИнформациюПоЧекуККМ(Источник);
	C6_СБПВызовСервера.ВернутьИнформациюПоЧекуККМ(ЧекИнфо);
	Если (ЧекИнфо.ЕстьКодОплаты) Тогда
		Если (Банк_ИнформацияПоQRКодуИПлатежу(ЧекИнфо, СтатусКодаОплаты, СуммаПлатежа)) Тогда
			Если (СтатусКодаОплаты = "SUCCESS") Тогда
				// добавим вид оплаты
				//C6_СБПВызовСервера.ДобавитьОплатуПоВидуВЧек(Форма, СуммаПлатежа);
				ДобавитьОплатуПоВидуВЧеке(Форма, СуммаПлатежа);
				ПоказатьПредупреждение(Новый ОписаниеОповещения, "Оплата по QR-коду получена", 6);
				C6_QRДисплей.ПоказатьРекламу();
				Возврат;
			ИначеЕсли (СтатусКодаОплаты = "DECLINED") Тогда
				// платеж отклонен
				ПоказатьПредупреждение(Новый ОписаниеОповещения, "Платеж по QR-коду ОТКЛОНЕН банком!", 6);
			Иначе
				// не оплачен
				ПоказатьПредупреждение(Новый ОписаниеОповещения, "Оплата по QR-коду не получена", 6);
			КонецЕсли;
		Иначе
			ПоказатьПредупреждение(Новый ОписаниеОповещения, "Не удалось получить информацию по оплате QR-кода!", 6);
		КонецЕсли;
		C6_QRДисплей.ПоказатьКод(C6_СБПВызовСервера.ВернутьURLКодаОплаты(ЧекИнфо.КодОплаты));
	Иначе
		ПоказатьПредупреждение(Новый ОписаниеОповещения, "QR-код не зарегистрирован, запросите QR-код!", 6);
	КонецЕсли;
КонецПроцедуры

//
Процедура Команда_СБПЗапроситьВозврат(Форма, Источник) Экспорт
	Перем ЧекИнфо;
	Перем СтатусВозврата, Сумма;
	Если (НЕ ЗначениеЗаполнено(Источник.Ссылка)) Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения, "Документ не записан!", 6);
		Возврат;
	КонецЕсли;
	ЧекИнфо = ВернутьИнформациюПоЧекуККМ(Источник);
	C6_СБПВызовСервера.ВернутьИнформациюПоЧекуККМ(ЧекИнфо);
	Если (ЧекИнфо.ЕстьКодОплаты) Тогда
		ЧекИнфо.Вставить("НазначениеПлатежа", НазначениеПлатежаПоЧеку(ЧекИнфо));
		Если (Банк_РегистрацияВозвратаПоПлатежу(ЧекИнфо, СтатусВозврата, Сумма)) Тогда
			Если (СтатусВозврата = "COMPLETED") Тогда
				// добавим вид оплаты
				//ДобавитьОплатуПоВидуВЧеке(Форма, Сумма);
				ПоказатьПредупреждение(Новый ОписаниеОповещения, "Возврат по QR-коду проведен", 6);
			ИначеЕсли (СтатусВозврата = "IN_PROGRESS") Тогда
				// возврат выполняется
			ИначеЕсли (СтатусВозврата = "DECLINED") Тогда
				// возврат отклонен
				ПоказатьПредупреждение(Новый ОписаниеОповещения, "Возврат по QR-коду ОТКЛОНЕН банком!", 6);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//
Процедура Команда_СБПСтатусВозврата(Форма, Источник) Экспорт
	Перем ЧекИнфо;
	Перем СтатусВозврата, Сумма;
	Если (НЕ ЗначениеЗаполнено(Источник.Ссылка)) Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения, "Документ не записан!", 6);
		Возврат;
	КонецЕсли;
	ЧекИнфо = ВернутьИнформациюПоЧекуККМ(Источник);
	C6_СБПВызовСервера.ВернутьИнформациюПоЧекуККМ(ЧекИнфо);
	Если (ЧекИнфо.ЕстьКодОплаты) Тогда
		Если (Банк_ИнформацияПоВозвратуПлатежа(ЧекИнфо, СтатусВозврата, Сумма)) Тогда
			Если (СтатусВозврата = "COMPLETED") Тогда
				// добавим вид оплаты
				//ДобавитьОплатуПоВидуВЧеке(Форма, Сумма);
				ПоказатьПредупреждение(Новый ОписаниеОповещения, "Возврат по QR-коду проведен", 6);
			ИначеЕсли (СтатусВозврата = "IN_PROGRESS") Тогда
				// возврат выполняется
				ПоказатьПредупреждение(Новый ОписаниеОповещения, "Подождите! Возврат по QR-коду еще не проведен", 6);
			ИначеЕсли (СтатусВозврата = "DECLINED") Тогда
				// возврат отклонен
				ПоказатьПредупреждение(Новый ОписаниеОповещения, "Возврат по QR-коду ОТКЛОНЕН банком!", 6);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//
Функция ВернутьИнформациюПоЧекуККМ(Источник) Экспорт
	Перем Рез;
	Перем Список, Товар;
	Рез = Новый Структура;
	Рез.Вставить("Ссылка", Источник.Ссылка);
	Рез.Вставить("НомерДок", Источник.Номер);
	Рез.Вставить("Магазин", Источник.Магазин);
	Рез.Вставить("Сумма", Источник.СуммаДокумента);
	Рез.Вставить("СтатусЧекаККМ", Источник.СтатусЧекаККМ); // Пробитый ПеречислениеСсылка.СтатусыЧековККМ
	Рез.Вставить("ВидОперации", Источник.ВидОперации); // Продажа ПеречислениеСсылка.ВидыОперацийЧекККМ
	Рез.Вставить("Организация", Источник.Организация);
	Если (Источник.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийЧекККМ.Возврат")) Тогда
		Рез.Вставить("ЧекККМПродажа", Источник.ЧекККМПродажа);
	КонецЕсли;
	Список = Новый Массив;
	Для Каждого Товар Из Источник.Товары Цикл
		Список.Добавить(Товар.Номенклатура);
	КонецЦикла;
	Рез.Вставить("СписокАртикулов", Список);
	// тут заглядываем в регистр
	Рез.Вставить("ЕстьКодОплаты", Ложь);
	Рез.Вставить("КодОплаты", "");
	Возврат Рез;
КонецФункции

//
Функция НазначениеПлатежаПоЧеку(ЧекИнфо)
	Перем Артикул, Список;
	Список = Новый Массив;
	Для Каждого Артикул Из ЧекИнфо.СписокАртикулов Цикл
		Список.Добавить(Артикул);
	КонецЦикла;
	Возврат СтрСоединить(Список, " ");
КонецФункции

//
Функция НайтиОплатуПоВидуВЧеке(Форма)
	Перем Рез;
	Перем ВидОплатыСБП;
	Перем СтруктураПоиска, Найдено;
	ВидОплатыСБП = C6_СБПВызовСервера.ВернутьВидОплатыДляСБП();
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ВидОплаты", ВидОплатыСБП);
	Найдено = Форма.Объект.Оплата.НайтиСтроки(СтруктураПоиска);
	Если (Найдено.Количество() > 0) Тогда
		Рез = Найдено[0].Сумма;
	КонецЕсли;
	Возврат Рез;
КонецФункции

//
Процедура ДобавитьОплатуПоВидуВЧеке(Форма, СуммаОплаты)
	Перем ВидОплатыСБП, НоваяСтрока;
	Перем СтруктураПоиска, Найдено;
	ВидОплатыСБП = C6_СБПВызовСервера.ВернутьВидОплатыДляСБП();
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("ВидОплаты", ВидОплатыСБП);
	Найдено = Форма.Объект.Оплата.НайтиСтроки(СтруктураПоиска);
	Если (Найдено.Количество() = 0) Тогда
		НоваяСтрока = Форма.Объект.Оплата.Добавить();
		НоваяСтрока.Сумма = СуммаОплаты;
		НоваяСтрока.ВидОплаты = ВидОплатыСБП;
		//НоваяСтрока.ДанныеПереданыВБанк = Истина; // должно быть доступно для редактирования
		Форма.Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

//
Функция Банк_РегистрацияQRКода(ЧекИнфо, Знач СуммаОплаты) Экспорт
	Перем Рез;
	Перем Параметры, Результат;
	
	Рез = Ложь;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", ЧекИнфо.Организация);
	Параметры.Вставить("Магазин", ЧекИнфо.Магазин);
	НастройкиAPI = C6_СБПВызовСервера.ПолучитьНастройки(Параметры);
	Параметры.Вставить("НомерЗаказа", Прав(ЧекИнфо.КодСклада, 2) + Сред(ЧекИнфо.НомерДок, 3));
	//Параметры.Вставить("Сумма", ЧекИнфо.Сумма);
	//сумма может быть не вся сумма чека
	Параметры.Вставить("Сумма", СуммаОплаты);
	//"paymentDetails": "Назначение платежа", вот тут можно через запятую перечислить номер чека как есть +артикула всех вещей
	// paymentDetails string <= 185 characters
	Параметры.Вставить("НазначениеПлатежа", "Оплата по чеку за товар: " + ЧекИнфо.НазначениеПлатежа);
	// additionalInfo string <= 140 characters
	Параметры.Вставить("ДопИнфо", "Магазин: " + ЧекИнфо.Магазин);
	Параметры.Вставить("СрокДействия", ТекущаяДата() + 86400);
	
	Если (C6_РайффайзенAPIКлиент.РегистрацияQRКода(Параметры, Результат)) Тогда
		Параметры = Новый Структура("Ссылка,КодОплаты,ДатаСоздания,СтатусКодаОплаты,ДатаОплаты,Сумма,ИзображениеКодаОплаты,URLКодаОплаты,ТранзакцияОплаты");
		Параметры.Ссылка = ЧекИнфо.Ссылка;
		Параметры.Сумма = ЧекИнфо.Сумма;
		Параметры.КодОплаты = Результат.КодОплаты;
		Параметры.ИзображениеКодаОплаты = СкачатьИзображение(Результат.URLИзображенияКода);
		Параметры.URLКодаОплаты = Результат.URLКодаОплаты;
		C6_СБПВызовСервера.ОбновитьСтатусКодаСБП(Параметры);
		Сообщить(Результат.КодОплаты);
		Сообщить(Результат.URLКодаОплаты);
		//Сообщить(Результат.СсылкаИзображения);
		ЧекИнфо.ЕстьКодОплаты = Истина;
		ЧекИнфо.КодОплаты =  Результат.КодОплаты;
		Рез = Истина;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

//
Функция Банк_ИнформацияПоQRКодуИПлатежу(ЧекИнфо, СтатусКодаОплаты, СуммаПлатежа)
	Перем Рез;
	Перем Параметры, Результат;
	Рез = Ложь;
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", ЧекИнфо.Организация);
	Параметры.Вставить("Магазин", ЧекИнфо.Магазин);
	Параметры.Вставить("КодОплаты", ЧекИнфо.КодОплаты);
	Если (C6_РайффайзенAPIКлиент.ИнформацияПоQRКоду(Параметры, Результат)) Тогда
		Параметры = Новый Структура("Ссылка,КодОплаты,ДатаСоздания,СтатусКодаОплаты,ДатаОплаты,Сумма,ИзображениеКодаОплаты,URLКодаОплаты,ТранзакцияОплаты");
		Параметры.Вставить("Организация", ЧекИнфо.Организация);
		Параметры.Вставить("Магазин", ЧекИнфо.Магазин);
		Параметры.Ссылка = ЧекИнфо.Ссылка;
		Параметры.КодОплаты = Результат.КодОплаты;
		Параметры.ИзображениеКодаОплаты = СкачатьИзображение(Результат.URLИзображенияКода);
		Параметры.URLКодаОплаты = Результат.URLКодаОплаты;
		Если (C6_РайффайзенAPIКлиент.ИнформацияПоПлатежу(Параметры, Результат)) Тогда
			//Enum: "SUCCESS" "DECLINED" "NO_INFO" "IN_PROGRESS"
			Сообщить(Результат.СтатусКодаОплаты);
			СтатусКодаОплаты = Результат.СтатусКодаОплаты;
			Параметры.СтатусКодаОплаты = Результат.СтатусКодаОплаты;
			СуммаПлатежа = Число(Результат.Сумма);
			Параметры.Сумма = СуммаПлатежа;
			Параметры.ТранзакцияОплаты = Результат.ИдТранзакции;
			Параметры.ДатаСоздания = C6_РайффайзенAPIКлиент.СтрокаВДату(Результат.ВремяСоздания);
			Параметры.ДатаОплаты = C6_РайффайзенAPIКлиент.СтрокаВДату(Результат.ВремяТранзакции);
			C6_СБПВызовСервера.ОбновитьСтатусКодаСБП(Параметры);
			Рез = Истина;
		КонецЕсли;
	КонецЕсли;
	Возврат Рез;
КонецФункции

//
Функция Банк_РегистрацияВозвратаПоПлатежу(ЧекИнфо, СтатусВозврата, Сумма)
	Перем Рез;
	Перем Параметры, Результат;
	Рез = Ложь;
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", ЧекИнфо.Организация);
	Параметры.Вставить("Магазин", ЧекИнфо.Магазин);
	Параметры.Вставить("НомерЗаказа", Прав(ЧекИнфо.КодСклада, 2) + Сред(ЧекИнфо.НомерДокПродажи, 3));
	Параметры.Вставить("НомерВозврата", Прав(ЧекИнфо.КодСклада, 2) + Сред(ЧекИнфо.НомерДок, 3));
	Параметры.Вставить("Сумма", ЧекИнфо.Сумма);
	// paymentDetails string <= 180 characters
	Параметры.Вставить("НазначениеПлатежа", "Чек возврата за товар: " + ЧекИнфо.НазначениеПлатежа);
	Если (C6_РайффайзенAPIКлиент.РегистрацияВозвратаПоПлатежу(Параметры, Результат)) Тогда
		Сообщить(Результат.СтатусВозврата);
		СтатусВозврата = Результат.СтатусВозврата;
		Сумма = Число(Результат.Сумма);
		Рез = Истина;
	КонецЕсли;
	Возврат Рез;
КонецФункции

//
Функция Банк_ИнформацияПоВозвратуПлатежа(ЧекИнфо, СтатусВозврата, Сумма)
	Перем Рез;
	Перем Параметры, Результат;
	Рез = Ложь;
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", ЧекИнфо.Организация);
	Параметры.Вставить("Магазин", ЧекИнфо.Магазин);
	Параметры.Вставить("НомерВозврата", Прав(ЧекИнфо.КодСклада, 2) + Сред(ЧекИнфо.НомерДок, 3));
	Если (C6_РайффайзенAPIКлиент.ИнформацияПоВозвратуПлатежа(Параметры, Результат)) Тогда
		Сообщить(Результат.СтатусВозврата);
		СтатусВозврата = Результат.СтатусВозврата;
		Сумма = Число(Результат.Сумма);
		Рез = Истина;
	КонецЕсли;
	Возврат Рез;
КонецФункции

///
Функция СкачатьИзображение(Знач УРЛИзображения)
	Перем Рез;
	Перем АдресСервера, АдресКартинки;
	Перем Соединение, Ответ;
	Если (РазложитьУРЛ(УРЛИзображения, АдресСервера, АдресКартинки)) Тогда
		Соединение = Новый HTTPСоединение(АдресСервера, Неопределено, Неопределено, Неопределено, Неопределено, 120, Новый ЗащищенноеСоединениеOpenSSL);
		Ответ = Соединение.Получить(Новый HTTPЗапрос(АдресКартинки));
		Если (Ответ.КодСостояния = 200) Тогда
			Рез = Новый Картинка(Ответ.ПолучитьТелоКакДвоичныеДанные());
			Рез.Записать("C:\Temp\qr-code.png");
		КонецЕсли;
	КонецЕсли;
	Возврат Рез;
КонецФункции

///
Функция РазложитьУРЛ(Знач УРЛ, Хост, Адрес)
	Перем Рез;
	Рез = Ложь;
	Поз = СтрНайти(УРЛ, "://");
	Если (Поз > 0) Тогда
		ТекСтр = Сред(УРЛ, Поз + 3);
		Поз = СтрНайти(ТекСтр, "/");
		Если (Поз > 0) Тогда
			Хост = Лев(ТекСтр, Поз - 1);
			Адрес = Сред(ТекСтр, Поз);
			Рез = Истина;
		КонецЕсли;
	КонецЕсли;
	Возврат Рез;
КонецФункции

////
Функция ПроверкаТелеметрона() Экспорт
	
	СкачатьИзображение("https://e-commerce.raiffeisen.ru/api/sbp/v1/qr/AD10006UHGT47JHS8JUB3U7GTTSH8M3O/image");
	Возврат Неопределено;
	
	KeUSB = Новый COMОбъект("MsCommLib.MsComm.1");
	KeUSB.CommPort = 1;
	KeUSB.Settings = "2400,N,8,1";
	KeUSB.Handshaking = 1;
	KeUSB.InputLen = 0;
	KeUSB.InBufferSize = 256;
	KeUSB.OutBufferSize = 256;
	KeUSB.RThreshold = 0;
	KeUSB.PortOpen = True;
	KeUSB.Output = "info" + Символы.ВК + Символы.ПС;
	Сообщить(KeUSB.Input);
	KeUSB.Output = "img=" + Символы.ВК + Символы.ПС;
	KeUSB.Output = "img=3" + Символы.ВК + Символы.ПС;
	KeUSB.PortOpen = False;
	
КонецФункции
