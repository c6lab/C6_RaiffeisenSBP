// (c) 2021 C6 Lab, Ramona Orlova, http://c6lab.org
// Все права сохранены.
////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ИмяПорта = C6_СБПВызовСервера.НастройкиИмяПорта();
#Если ТонкийКлиент ИЛИ ВебКлиент Тогда
	C6_QRДисплей.УдалитьДрайвер(); // для обеспечения обновления компоненты
#КонецЕсли
	Если (НЕ C6_QRДисплей.Доступен()) Тогда
		Сообщить("QR-дисплей не доступен!");
		Элементы.Проверить.Доступность = Ложь;
		Элементы.ПоказатьКод.Доступность = Ложь;
		Элементы.Очистить.Доступность = Ложь;
	Иначе
		QRКод = "https://c6lab.org/";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если (НЕ ЗавершениеРаботы) Тогда
		C6_СБПВызовСервера.ЗапомнитьНастройкиИмяПорта(ИмяПорта);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Проверить(Команда)
	Перем Дисплей;
	Пока (Истина = Истина) Цикл
		// тест создания объекта
		Попытка
			Дисплей = Новый("AddIn.C6DisplayQR.ComUtil");
		Исключение
			Сообщить(ОписаниеОшибки());
			Сообщить("Ошибка создания объекта компоненты ""C6DisplayQR.ComUtil""");
			Прервать;
		КонецПопытки;
		// тест записи свойства
		Сообщить("=> " + ИмяПорта);
		Попытка
			Дисплей.ИмяПорта = ИмяПорта;
		Исключение
			Сообщить(ОписаниеОшибки());
			Сообщить("Ошибка записи свойства ""ИмяПорта""");
			Прервать;
		КонецПопытки;
		// тест чтения свойства
		Попытка
			Сообщить("<= " + Дисплей.ИмяПорта);
		Исключение
			Сообщить(ОписаниеОшибки());
			Сообщить("Ошибка чтения свойства ""ИмяПорта""");
			Прервать;
		КонецПопытки;
		// тест выполнения метода - открытие порта
		Попытка
			Рез = Дисплей.ОткрытьПорт();
			Если (Рез) Тогда
				Сообщить(ИмяПорта + " открыт");
			Иначе
				Сообщить(ИмяПорта + " НЕ открыт");
			КонецЕсли;
		Исключение
			Сообщить(ОписаниеОшибки());
			Сообщить("Ошибка выполения метода ""ОткрытьПорт""");
			Прервать;
		КонецПопытки;
		Если (Рез) Тогда
			// тест выполнения метода - отправка QR кода
			Попытка
				Рез = Дисплей.ОтправитьQRКод("https://c6lab.org");
				Если (Рез) Тогда
					Сообщить(ИмяПорта + " отправлен QR-код");
				КонецЕсли;
			Исключение
				Сообщить(ОписаниеОшибки());
				Сообщить("Ошибка выполения метода ""ОтправитьQRКод""");
				Прервать;
			КонецПопытки;
			// тест выполнения метода - закрытие порта
			Попытка
				Дисплей.ЗакрытьПорт();
				Сообщить(ИмяПорта + " закрыт");
			Исключение
				Сообщить(ОписаниеОшибки());
				Сообщить("Ошибка выполения метода ""ЗакрытьПорт""");
				Прервать;
			КонецПопытки;
		КонецЕсли;
		Прервать;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКод(Команда)
	Перем Дисплей;
	Попытка
		Дисплей = Новый("AddIn.C6DisplayQR.ComUtil");
		Дисплей.ИмяПорта = ИмяПорта;
		Если (Дисплей.ОткрытьПорт()) Тогда
			Дисплей.ОтправитьQRКод(QRКод);
			Дисплей.ЗакрытьПорт();
		КонецЕсли;
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	Перем Дисплей;
	Попытка
		Дисплей = Новый("AddIn.C6DisplayQR.ComUtil");
		Дисплей.ИмяПорта = ИмяПорта;
		Если (Дисплей.ОткрытьПорт()) Тогда
			Дисплей.ОчиститьДисплей();
			Дисплей.ЗакрытьПорт();
		КонецЕсли;
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры
